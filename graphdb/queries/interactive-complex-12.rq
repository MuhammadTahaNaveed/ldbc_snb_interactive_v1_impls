PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select ?fr ?first ?last (group_concat(distinct ?tagname;
        separator=", ") as ?tagnames) (count (distinct ?reply) as ?count)
where {
    {
        sn:pers%personId% snvoc:directKnows ?fr .
        ?fr snvoc:firstName ?first .
        ?fr snvoc:lastName ?last .
        ?reply snvoc:hasCreator ?fr .
        ?reply snvoc:replyOf ?org_post .
        filter (!exists {
                ?org_post snvoc:replyOf ?xx
            }) .
        ?org_post snvoc:hasTag ?tag .
        ?tag foaf:name ?tagname .
        ?tag a ?type.
        ?type rdfs:subClassOf* ?type1 .
        ?type1 rdfs:label "%tagClassName%" .
        filter(sn:pers%personId% != ?fr)
    }
}
group by ?fr ?first ?last
order by desc(?count) ?fr
limit 20