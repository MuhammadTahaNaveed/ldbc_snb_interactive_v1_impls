PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX onto: <http://www.ontotext.com/path#>

select distinct ?fr ?first ?last ?post ?content ?date
where {
    {
        select distinct ?fr
            where {
                VALUES (?source ?destination) {
                    (sn:pers%personId% undef)
                }
                SERVICE <http://www.ontotext.com/path#search> {
                    <urn:path> onto:findPath onto:allPaths ;
                               onto:sourceNode ?source ;
                               onto:destinationNode ?destination ;
                               onto:maxPathLength "2"^^xsd:int;
                               onto:startNode ?start ;
                               onto:endNode ?fr .
                    SERVICE <urn:path> {
                       ?fr snvoc:knows/snvoc:hasPerson ?start.
                    }
                }
            }
    }
    ?post snvoc:hasCreator ?fr.
    ?post snvoc:creationDate ?date.
    filter (?date < "%maxDate%"^^xsd:dateTime).
        {
            ?post snvoc:content ?content
        } union {
            ?post snvoc:imageFile ?content
        }
        ?fr snvoc:firstName ?first .
    ?fr snvoc:lastName ?last .
}
order by desc (?date) ?post
limit 20
