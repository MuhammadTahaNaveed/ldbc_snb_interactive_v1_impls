PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX onto: <http://www.ontotext.com/path#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

select ?tagname (count (distinct ?post) as ?count)
where {
    {
        select distinct ?fr
        where {
            VALUES (?source ?destination) {
                (sn:pers%personId% undef)
            }
            SERVICE <http://www.ontotext.com/path#search> {
                <urn:path> onto:findPath onto:allPaths ;
                           onto:sourceNode ?source ;
                           onto:destinationNode ?destination ;
                           onto:maxPathLength "2"^^xsd:int;
                           onto:startNode ?start ;
                           onto:endNode ?fr .
                SERVICE <urn:path> {
                  ?start snvoc:knows/snvoc:hasPerson ?fr .
                }
            }
        }
    } .
     ?post snvoc:hasCreator ?fr .
     ?post snvoc:hasTag/foaf:name "%tagName%" .

    ?post rdf:type snvoc:Post .
    ?post snvoc:hasTag ?tag .
    ?tag foaf:name ?tagname .
    filter(?tagname != "%tagName%")
}
group by ?tagname
order by desc(?count) ?tagname
limit 10
