PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX onto: <http://www.ontotext.com/path#>
select ?title  (SUM(?friendHasPostOrNot) AS ?count)
where {
    {
        select * where {
            {
                select distinct ?fr
                where {
                    {
                        sn:pers%personId% snvoc:directKnows ?fr.
                    } union {
                        sn:pers%personId% snvoc:directKnows/snvoc:directKnows ?fr.
                        filter ( ?fr != sn:pers%personId%)
                    }
                }
            }
            ?forum snvoc:hasMember ?mem .
            ?mem snvoc:joinDate ?date .
            ?mem snvoc:hasPerson ?fr .
            filter (?date > "%minDate%"^^xsd:dateTime) .
        }
    }
    OPTIONAL{
        ?post snvoc:hasCreator ?fr .
        ?forum snvoc:containerOf ?post .
        BIND( 1 AS ?friendHasPost )
    }
    ?forum snvoc:title ?title.
    ?forum snvoc:id ?forumId .
    BIND( COALESCE(?friendHasPost, 0) AS ?friendHasPostOrNot )
}
group by ?title ?forumId
order by desc(?count) ?forumId
limit 20