PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
select ?fr ?fname ?lname ?first ?second ?sum where {
    {
        select ?fr (sum(?one) as ?first) (sum(?two) as ?second) (?first + ?second as ?sum)
        where {
            {
                select distinct ?fr {
                    {
                        sn:pers%personId% snvoc:directKnows  ?fr.
                    } union {
                        sn:pers%personId% snvoc:directKnows/snvoc:directKnows ?fr.
                        filter (?fr != sn:pers%personId%)
                    }
                    ?fr snvoc:isLocatedIn ?city .
                    ?city snvoc:isPartOf ?cnt .
                    filter (?cnt != dbpedia:%countryXName% && ?cnt != dbpedia:%countryYName%)
                }
            }
            {
                ?post snvoc:hasCreator ?fr .
                ?post snvoc:isLocatedIn dbpedia:%countryXName% .
                ?post snvoc:creationDate ?date .
            } union {
                ?post1 snvoc:hasCreator ?fr .
                ?post1 snvoc:isLocatedIn dbpedia:%countryYName% .
                ?post1 snvoc:creationDate ?date .
            }
            bind ("%startDate%"^^xsd:dateTime + "P%durationDays%D"^^xsd:duration as ?dateAdded).
            filter (?date >= "%startDate%"^^xsd:dateTime && ?date < ?dateAdded) .
            BIND ((IF (bound(?post), 1,0)) AS ?one)
            BIND ((IF (bound(?post1), 1,0)) AS ?two)
        } group by ?fr
    }
    filter(?first != 0 && ?second != 0)
    ?fr snvoc:firstName ?fname .
    ?fr snvoc:lastName ?lname .
}
order by desc(?sum) ?fr
limit 20