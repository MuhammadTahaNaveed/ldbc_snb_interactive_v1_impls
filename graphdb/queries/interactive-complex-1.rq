PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX onto: <http://www.ontotext.com/path#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
select ?fr ?lastName ?mindist ?bday ?since ?gen ?browser ?locationIP (group_concat(distinct ?email;
        separator=", ") as ?emails) (group_concat(distinct ?lng;
        separator=", ") as ?lngs) ?based (group_concat(distinct concat(?study_name, " ", str(?study_year), " ", ?study_country);
        separator=", ") as ?studyAt) (group_concat(distinct concat(?work_name, " ", str(?work_year), " ", ?work_country);
        separator=", ") as ?workAt)
where {
    ?fr snvoc:firstName "%firstName%" .
    ?fr snvoc:lastName ?lastName .
    ?fr snvoc:birthday ?bday .
    ?fr snvoc:isLocatedIn ?basedURI .
    ?basedURI foaf:name ?based .
    ?fr snvoc:creationDate ?since .
    ?fr snvoc:gender ?gen .
    ?fr snvoc:locationIP ?locationIP .
    ?fr snvoc:browserUsed ?browser .
    ?fr snvoc:email ?email .
    ?fr snvoc:speaks ?lng .
    optional {
        ?fr snvoc:studyAt ?study .
        ?study snvoc:classYear ?study_year .
        ?study snvoc:hasOrganisation ?study_org .
        ?study_org snvoc:isLocatedIn ?study_countryURI.
        ?study_countryURI foaf:name ?study_country .
        ?study_org foaf:name ?study_name .
    }
    optional {
        ?fr snvoc:workAt ?work .
        ?work snvoc:workFrom ?work_year .
        ?work snvoc:hasOrganisation ?work_org .
        ?work_org snvoc:isLocatedIn ?work_countryURI.
        ?work_countryURI foaf:name ?work_country .
        ?work_org foaf:name ?work_name .
    }
    {
        select distinct ?fr (min(?index + 1) as ?mindist) where {
            VALUES (?source ) {
                ( sn:pers%personId% )
            }
            ?destination snvoc:firstName "%firstName%" .
            filter (?destination != sn:pers%personId%)
            SERVICE <http://www.ontotext.com/path#search> {
                <urn:path> onto:findPath onto:shortestPath ;
                           onto:sourceNode ?source ;
                           onto:destinationNode ?destination ;
                           onto:resultBindingIndex ?index ;
                           onto:maxPathLength "3"^^xsd:int;
                           onto:startNode ?start ;
                           onto:endNode ?fr .
                SERVICE <urn:path> {
                    ?start snvoc:directKnows ?fr.
                }
            }
        } group by ?fr
    }
}
group by ?fr ?lastName ?bday ?since ?gen ?browser ?locationIP ?based ?mindist
order by ?mindist ?lastName ?fr
limit 20