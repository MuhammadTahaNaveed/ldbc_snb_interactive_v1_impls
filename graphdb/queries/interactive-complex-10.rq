PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX onto: <http://www.ontotext.com/path#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

select distinct ?foaf ?firstName ?lastName (if(sum(?match) = -1, 0, (sum(?match) - sum (if(?match = 0, 1, 0)))) as ?similarity) ?gender ?cityName where {
    {
        select distinct ?foaf
        where {
            VALUES (?source ?destination) {
                (sn:pers%personId% undef)
            }
            SERVICE <http://www.ontotext.com/path#search> {
                <urn:path> onto:findPath onto:allPaths ;
                           onto:sourceNode ?source ;
                           onto:destinationNode ?destination ;
                           onto:minPathLength "2"^^xsd:int;
                           onto:maxPathLength "2"^^xsd:int;
                           onto:startNode ?start ;
                           onto:endNode ?foaf .
                SERVICE <urn:path> {
                    ?start snvoc:knows/snvoc:hasPerson ?foaf.
                }
            }
            minus {
                ?source snvoc:knows/snvoc:hasPerson ?foaf
            }
        }
    }
    ?foaf snvoc:firstName ?firstName ;
          snvoc:lastName ?lastName ;
          snvoc:gender ?gender ;
          snvoc:birthday ?birthday ;
          snvoc:isLocatedIn ?city .
    ?city foaf:name ?cityName .
    BIND (IF(%month% = 12, 1, %month% + 1) AS ?nextMonth)
   	FILTER ((MONTH(?birthday) = %month% && DAY(?birthday) >= 21) || (MONTH(?birthday) = (?nextMonth) && DAY(?birthday) < 22)) .
    optional {
	  ?post snvoc:hasCreator ?foaf ;
          rdf:type snvoc:Post .
       bind (if (
            exists {
			sn:pers%personId% snvoc:hasInterest ?tag .
        	?post snvoc:hasTag ?tag .
			}, 1, 0) as ?score)
    }
    bind(coalesce(?score, -1) as ?match)
}
group by ?foaf ?firstName ?lastName ?gender ?cityName
order by desc(?similarity) ?foaf
limit 10